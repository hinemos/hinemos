apply plugin: 'java'
apply plugin: 'eclipse'

project.group = theGroup
version = theVersion

sourceCompatibility = theSourceCompatibility
targetCompatibility = theSourceCompatibility

repositories{
	mavenCentral()
}
dependencies{
	compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
	compile group: 'log4j', name: 'log4j', version: '1.2.17'
	compile files('../common/build/libs/HinemosCommon.jar')
	compile files("../manager/build/libs/AgentWS.jar")

	// JNA library
	compile group: 'net.java.dev.jna', name: 'jna', version: '4.3.0'
	compile group: 'net.java.dev.jna', name: 'jna-platform', version: '4.3.0'

	// For Manager Cli
	compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
}

eclipse{
	project{
		name = theJarName
	}
}

sourceSets{
	manager_cli{
		java{
			srcDir 'src_manager_cli'
		}
		compileClasspath += sourceSets.main.runtimeClasspath
	}
}

tasks.withType(JavaCompile){
	options.encoding = 'UTF-8'
}
def manifestDef = {
	attributes(
		'Hinemos-Version': version,
		'Build-Date': new Date().format('yyyy-MM-dd HH:mm:ss', TimeZone.getTimeZone('UTC')))
}

jar{
	manifest(manifestDef)
}


// Doc
task sourcesJar(type: Jar, dependsOn: classes){
	classifier = 'sources'
	from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc){
	classifier = 'javadoc'
	from javadoc.destinationDir
}
artifacts{
	archives sourcesJar
	//archives javadocJar
}

task exportAgent(type: Copy, dependsOn: build){
	description 'Rename the jar file to its original name then you can replace to Hinemos directly.'
	//from "${rootProject.buildDir}/${rootProject.libsDirName}"
	//into "${rootProject.buildDir}/${rootProject.libsDirName}"
	from "${buildDir}/${libsDirName}"
	into "${buildDir}/${libsDirName}"
	rename "${project.name}-$version", theJarName
}

// Build HinemosManagerCli
task exportManagerCli(type: Jar){
	description 'Generate HinemosManagerCli.jar'
	archiveName = 'HinemosManagerCli.jar'
	from sourceSets.manager_cli.output

	manifest(manifestDef)
}

task export{
	description 'Generate both HinemosAgent.jar and HinemosManagerCli.jar'
	dependsOn exportAgent
	dependsOn exportManagerCli
}

